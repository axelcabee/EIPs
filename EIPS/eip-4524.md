---
eip: 4524
title: Safer ERC-20
description: Extends ERC-20 with ERC-165 and adds ERC-721-like safeTransfer
author: William Schwab (@wschwab), Gavin John (@Pandapip1)
discussions-to: https://ethereum-magicians.org/t/why-isnt-there-an-erc-for-safetransfer-for-erc20/7604
status: Review
type: Standards Track
category: ERC
created: 2021-12-05
requires: 20, 165, 721
---

## Abstract

This standard adds [ERC-721](./eip-721.md) and [ERC-1155](./eip-1155.md)-based functionality to [ERC-20](./eip-20.md) tokens, such as transfer hooks and [ERC-165](./eip-165.md) introspection.

## Specification

The key words “MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “MAY”, and “OPTIONAL” in this document are to be interpreted as described in RFC 2119.

### ERC-20 Extension

As per [ERC-165](./eip-165.md), compliant contracts MUST return `true` when `supportsInterface(0x534f5876)` is called. Compliant contracts MUST also implement the below interface in additon to [ERC-20](./eip-20.md):

```solidity
pragma solidity 0.8.10;

import './IERC20.sol';
import './IERC165.sol';

/// @notice Safer ERC-20
/// @dev    The ERC-165 interfaceId for this interface is `0x534f5876`
interface SaferERC20 is IERC20, IERC165 {
  /// @notice         Transfers `amount` tokens to `to`, with checks to ensure that `to` supports receiving the tokens being sent
  /// @dev            If `to` is neither an EOA (i.e. has a code size of zero), nor returns `0x4fc35859` when `onERC20Received(msg.sender, msg.sender, amount, empty)` is called, the transfer MUST fail
  ///                 The above check MUST be in addition to the normal ERC-20 `transfer` checks
  /// @param  to      The account to which to send the tokens
  /// @param  amount  The amount of tokens to send
  function safeTransfer(address to, uint256 amount) external returns (bool);
  
  /// @notice         Transfers `amount` tokens to `to`, with checks to ensure that `to` supports receiving the tokens being sent
  /// @dev            If `to` is neither an EOA (i.e. has a code size of zero), nor returns `0x4fc35859` when `onERC20Received(msg.sender, msg.sender, amount, data)` is called, the transfer MUST fail
  ///                 The above check MUST be in addition to the normal ERC-20 `transfer` checks
  /// @param  to      The account to which to send the tokens
  /// @param  amount  The amount of tokens to send
  /// @param  data    The data to send with the callback
  function safeTransfer(address to, uint256 amount, bytes memory data) external returns (bool);
  
  /// @notice         Transfers `amount` tokens to `to` from `from`, with checks to ensure that `to` supports receiving the tokens being sent
  /// @dev            If `to` is neither an EOA (i.e. has a code size of zero), nor returns `0x4fc35859` when `onERC20Received(msg.sender, msg.sender, amount, empty)` is called, the transfer MUST fail
  ///                 The above check MUST be in addition to the normal ERC-20 `transferFrom` checks
  /// @param  to      The account to which to send the tokens
  /// @param  amount  The amount of tokens to send
  function safeTransferFrom(address from, address to, uint256 amount) external returns (bool);
  
  /// @notice         Transfers `amount` tokens to `to` from `from`, with checks to ensure that `to` supports receiving the tokens being sent
  /// @dev            If `to` is neither an EOA (i.e. has a code size of zero), nor returns `0x4fc35859` when `onERC20Received(msg.sender, msg.sender, amount, data)` is called, the transfer MUST fail
  ///                 The above check MUST be in addition to the normal ERC-20 `transferFrom` checks
  /// @param  to      The account to which to send the tokens
  /// @param  amount  The amount of tokens to send
  /// @param  data    The data to send with the callback
  function safeTransferFrom(address from, address to, uint256 amount, bytes memory data) external returns (bool);
}
```

### Token Receiver Interface

A contract accepting safe transfers MUST implement the following if it wishes to accept safe transfers:

```solidity
pragma solidity 0.8.10;

import './IERC165.sol';

interface ERC20Receiver is IERC165 {
  /// @notice Callback function when `safeTransfer` is called
  /// @dev    MUST return the function selector (`0x4fc35859`) to accept the transaction, or revert to cancel it
  ///         Contracts MUST NOT return an invalid selector to cancel transactions
  ///         To determine which ERC-20 token was received, use `msg.sender`
  /// @param  _operator The account that called `safeTransfer` (i.e. the msg.sender of the parent function)
  /// @param  _from     The account from which the tokens were sent
  /// @param  _amount   How many tokens were sent
  /// @param  _data     Free-form user-provided data
  function onERC20Received(
    address _operator,
    address _from,
    uint256 _amount,
    bytes _data
  ) external returns (bytes4);
}
```

## Rationale

This ERC is designed to be minimal and straightforward, adding a number of features that the authors feel are missing from ERC-20.

Adding ERC-165 to ERC-20 is useful for a number of applications that require introspection, and outside of a minimal amount of code that increases the contract size and deployment cost, carries no downside.

The names `safeTransfer` and `safeTransferFrom` functions, based on ERC-721 and ERC-1155, are well-recognized. Keeping identical naming conventions for analogous behavior is reasonable. The benefits of validating receiving contracts is well documented in the Rationales of both ERC-721 and ERC-1155.

<Another design choice made in ERC-721 and ERC-1155 is the inclusion of a metadata URI for tokens, allowing them to easily provide a logo and other details. This has not been included because it is standardized by `ERC-1046`. A recommendation is made to use that ERC.

## Backwards Compatibility

This ERC is compatible with ERC-20, as there are no function selector conflicts. However, existing contracts that expect to recieve ERC-20 tokens and do not support the `ERC20Receiver` interface will not be able to use `safeTransfer`.

## Security Considerations

`onERC20Received` is a callback function. Callback functions have been exploited in the past as a reentrancy vector, and care should be taken to make sure implementations are not vulnerable.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
